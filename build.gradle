buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'

    id 'org.ajoberstar.grgit' version '1.7.2'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'org.unbroken-dome.test-sets' version '2.0.3'
}

group 'com.finplant.ib'
version '2.3.6'

sourceCompatibility = 1.12
targetCompatibility = 1.12

repositories {
    mavenCentral()
}

dependencies {
    implementation files('libs/TwsApi_debug.jar')

    implementation 'org.slf4j:slf4j-api:1.7.+'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'com.google.guava:guava:27.1-jre'

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.objenesis:objenesis:2.6'
    testImplementation 'net.bytebuddy:byte-buddy:1.9.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.+'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}

testSets {
    integrationTest {
        dirName = 'test-integration'
    }
}

shadowJar {
    baseName = 'ib_client'
    classifier = null
}

javadoc {
    excludes = ["com/finplant/ib/impl/**"]
    options.stylesheetFile = new File(projectDir, "docs/stylesheet.css");

    options.overview = new File(projectDir, "docs/overview.html");
    options.tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
    options.addBooleanOption("-allow-script-in-comments", true)
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact packageJavadoc
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'gcs://finplant-maven-repo/releases'
            def snapshotsRepoUrl = 'gcs://finplant-maven-repo/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}